인터럽트가 발생하면 인터럽트 핸들러를 호출해서 해당 인터럽트를 처리한다고 했었다([[인터럽트]] 내용 참고). ARM 아키텍처에서는 IRQ(Interrupt Request) 발생 시, 인터럽트 벡터 테이블의 `vector_irq` 엔트리가 실행될 수 있으며, 이후 커널에서 `__irq_svc` 같은 인터럽트 핸들러가 호출되어 해당 인터럽트를 처리한다.

###### 인터럽트 벡터
현재 사용 중인 라즈베리 파이 5는 ARMv8 아키텍처 기반으로 동작한다. 이것을 기준으로 인터럽트 벡터의 정의 방법을 살펴보자.

먼저 `entry.S`[^1] 파일에서 익셉션 벡터 테이블을 설정한다. 
```assembly
// https://github.com/raspberrypi/linux/tree/rpi-6.6.y/arch/arm64/kernel/entry.S
...
/*
 * Exception vectors.
 */
	.pushsection ".entry.text", "ax"

	.align	11
SYM_CODE_START(vectors)
	kernel_ventry	1, t, 64, sync		// Synchronous EL1t
	kernel_ventry	1, t, 64, irq		// IRQ EL1t
	kernel_ventry	1, t, 64, fiq		// FIQ EL1t
	kernel_ventry	1, t, 64, error		// Error EL1t

	kernel_ventry	1, h, 64, sync		// Synchronous EL1h
	kernel_ventry	1, h, 64, irq		// IRQ EL1h
	kernel_ventry	1, h, 64, fiq		// FIQ EL1h
	kernel_ventry	1, h, 64, error		// Error EL1h

	kernel_ventry	0, t, 64, sync		// Synchronous 64-bit EL0
	kernel_ventry	0, t, 64, irq		// IRQ 64-bit EL0
	kernel_ventry	0, t, 64, fiq		// FIQ 64-bit EL0
	kernel_ventry	0, t, 64, error		// Error 64-bit EL0

	kernel_ventry	0, t, 32, sync		// Synchronous 32-bit EL0
	kernel_ventry	0, t, 32, irq		// IRQ 32-bit EL0
	kernel_ventry	0, t, 32, fiq		// FIQ 32-bit EL0
	kernel_ventry	0, t, 32, error		// Error 32-bit EL0
SYM_CODE_END(vectors)
...
```
`.pushsection` 명령을 이용해 익셉션 벡터를 위한 섹션을 생성하고, `vectors` 레이블에 `kernel_ventry` 매크로를 이용해 익셉션 벡터가 인터럽트 핸들러의 진입점이 되도록 한다. 다시 말하면, 익셉션 벡터는 특정 예외 발생 시 실행할 핸들러의 주소를 포함하는 테이블이다.

`kernel_ventry` 매크로 코드를 살펴보자.(중간의 전처리 코드는 생략했다.)
```assembly
// https://github.com/raspberrypi/linux/tree/rpi-6.6.y/arch/arm64/kernel/entry.S
...
.macro kernel_ventry, el:req, ht:req, regsize:req, label:req
	.align 7
.Lventry_start\@:
	.if	\el == 0
	/*
	 * This must be the first instruction of the EL0 vector entries. It is
	 * skipped by the trampoline vectors, to trigger the cleanup.
	 */
	b	.Lskip_tramp_vectors_cleanup\@
	.if	\regsize == 64
	mrs	x30, tpidrro_el0
	msr	tpidrro_el0, xzr
	.else
	mov	x30, xzr
	.endif
.Lskip_tramp_vectors_cleanup\@:
	.endif

	sub	sp, sp, #PT_REGS_SIZE
	...
	b	el\el\ht\()_\regsize\()_\label
.org .Lventry_start\@ + 128	// Did we overflow the ventry slot?
	.endm
...
```
`kernel_ventry` 매크로는 4개의 매개변수를 받는다:
- `el` : 실행 레벨(EL0, EL1, EL2 등)
- `ht` : 실행 컨텍스트(핸들러 컨텍스트 `h`, 스레드 컨텍스트 `t`)
- `regsize` : 레지스터 크기 (AArch64 - `64` 또는 AArch32 - `32`)
- `label` : 실제 익셉션(또는 인터럽트) 유형으로, 최종적으로 점프할 핸들러 이름을 조합하는 데에 쓰인다.

이 매크로에서는 익셉션 벡터 엔트리를 128 byte 단위로 정렬한다(`.align 7`). `.Lventry_start\@`는 매크로 사용 시마다 유일한 레이블 이름[^2]이 만들어지고, 이것이 이 엔트리의 시작 위치가 된다.

`.if \el == 0` 부분 부터는 EL0(유저 모드)에서 발생한 예외일 때만 실행되는 부분이다. 일단은 간단하게 유저 스레드 관련 레지스터를 정리하는 부분이라고 알고 있자.

다음으로는 `sub sp, sp, #PT_REGS_SIZE` 명령으로 스택 포인터 `sp`를 감소시켜 예외 처리를 위해 필요한 스택 공간을 마련한다.

다음으로는 매크로 인자를 조합해 최종 점프할 핸들러 이름을 만든 뒤, 그 위치로 분기한다.
```
\el = 1, \ht = t, \regsize = 64, \label = sync 인 경우
b el\el\ht\()_\regsize\()_\label -> b el1t_64_sync
```

이 매크로가 인터럽트를 위해 사용된 부분을 다시 보면, 각각의 경우에 대해 인터럽트 벡터가 정의되는 것을 확인할 수 있다. 
```assembly
...
kernel_ventry	1, t, 64, irq		// IRQ EL1t
...
kernel_ventry	1, h, 64, irq		// IRQ EL1h
...
kernel_ventry	0, t, 64, irq		// IRQ 64-bit EL0
...
kernel_ventry	0, t, 32, irq		// IRQ 32-bit EL0
```

`entry.S` 파일에는 실행 레벨 EL2 이상에 대한 코드가 보이지 않는데, 이것은 `hyp-entry.S` 파일에서 찾을 수 있었다. 물론 여기도 EL3 코드는 찾을 수 없는데, 이것은 지금 사용하고 있는 리눅스 커널(rpi-6.6y에서는 사용하고 있지 않기 때문에 없는 것으로 보인다.
```
// https://github.com/raspberrypi/linux/tree/rpi-6.6.y/arch/arm64/kvm/hyp/hyp-entry.S
...
SYM_CODE_START(__kvm_hyp_vector)
	invalid_vect	el2t_sync_invalid	// Synchronous EL2t
	invalid_vect	el2t_irq_invalid	// IRQ EL2t
	invalid_vect	el2t_fiq_invalid	// FIQ EL2t
	invalid_vect	el2t_error_invalid	// Error EL2t

	valid_vect	el2_sync		// Synchronous EL2h
	invalid_vect	el2h_irq_invalid	// IRQ EL2h
	invalid_vect	el2h_fiq_invalid	// FIQ EL2h
	valid_vect	el2_error		// Error EL2h

	valid_vect	el1_sync		// Synchronous 64-bit EL1
	valid_vect	el1_irq			// IRQ 64-bit EL1
	valid_vect	el1_fiq			// FIQ 64-bit EL1
	valid_vect	el1_error		// Error 64-bit EL1

	valid_vect	el1_sync		// Synchronous 32-bit EL1
	valid_vect	el1_irq			// IRQ 32-bit EL1
	valid_vect	el1_fiq			// FIQ 32-bit EL1
	valid_vect	el1_error		// Error 32-bit EL1
SYM_CODE_END(__kvm_hyp_vector)
...
```
매크로는 하이퍼바이저 모드를 다루는 것이라 조금 다른데, 이것은 나중에 알아보자.

> 핸들러 컨텍스트 vs 스레드 컨텍스트
> 같은 실행 레벨(EL)에서도 해당 EL에서 어떤 스택 포인터를 사용하는지에 따라 나뉜다.
> - `sp_ELx` : 해당 EL 전용 스택 포인터로, `sp_el1`, `sp_el2` 등이 있다. 특권 모드[^privileged_mode] 코드를 처리할 때 주로 사용한다. -> 핸들러 컨텍스트
> - `sp_EL0` : 유저 모드용 스택 포인터로, 유저 프로세스가 사용하는 스택이다. -> 스레드 컨텍스트
> 핸들러 컨텍스트는 해당 EL에서 전용 스택 포인터를 사용하여 코드를 실행하고, 스레드 컨텍스트는 무조건 유저 모드 스택(`sp_EL0`)을 사용하여 코드를 실행하는 모드이다.
> 
> 핸들러 컨텍스트를 사용하면, 전용 스택을 사용하므로 유저 스택과 분리되어, 인터럽트나 예외 처리 중 유저 스택에 접근하지 않고 안정적으로 핸들러가 동작할 수 있다. 하지만 간단한 시스템에서는 컨텍스트 전환을 단순하게 하기 위해 스레드 컨텍스트를 활용해 유저 스택을 공용으로 쓰도록 할 수 있다. 물론, 이것은 전용 스택을 사용하는 것보다 보안적인 측면이나 안정성 측면에서 좋지 않을 수 있다.
> 
> 정리하면, 별도 스택을 두면 스택 공간이 더 필요하지만, 더 안전하게 사용할 수 있다(핸들러 컨텍스트). 반면, 하나의 스택을 공유하면 메모리는 절약되지만, 보안이나 안정성이 취약할 수 있다(스레드 컨텍스트).


[^1]: 어셈블리 코드로 작성된 소스 파일로, `#define`이나 `#include` 같은 C 언어의 전처리기(`cpp`)를 지원한다. 소문자 `.s` 파일도 어셈블리 소스 코드지만, C 언어의 전처리기를 지원하지 않는다.
[^2]: 유일한 레이블 이름은 `\@`로 결정된다. 이것은 매크로 내부에서 사용되지 않는 어떤 숫자로 변환된다.